apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    // https://developer.android.com/studio/releases/build-tools.html
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "info.hossainkhan.dailynewsheadlines"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 22
        versionName "1.17-tv"
        versionNameSuffix "-${gitSha()}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        multiDexEnabled true
    }
    buildTypes {
        debug {
            // Turn off optimization for faster debug builds
            shrinkResources false
            minifyEnabled false
        }

        release {
            // Disable shrink resource because of following known issue with gradle tools 2.2.2
            // http://stackoverflow.com/questions/36540676/build-intermediates-res-resources-anzhi-debug-stripped-ap-specified-for-prope
            shrinkResources false
            minifyEnabled false // Proguard config is not customized yet.
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        /*
         * `InvalidPackage` warning from dagger. Fixed issue using following sources:
         * http://stackoverflow.com/questions/21827004/gradle-dagger-lint-ignore-by-package
         * https://github.com/square/okio/issues/58
         */
        warning 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibilityVersion
        targetCompatibility rootProject.ext.javaTargetCompatibilityVersion
    }
}

jacocoAndroidUnitTestReport {
    excludes += ['**/AutoValue_*.*',
                 '**/*$ViewBinder*.*', /* Based on https://github.com/JakeWharton/butterknife/issues/289 */
                 '**/*JavascriptBridge.class']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Core module for the app
    implementation project(':core-lib')

    implementation "com.android.support:leanback-v17:$rootProject.supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"

    // Needed for leanback preference
    implementation "com.android.support:preference-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:preference-leanback-v17:$rootProject.supportLibraryVersion"
    implementation "com.android.support:preference-v14:$rootProject.supportLibraryVersion"

    // Firebase CORE
    // https://firebase.google.com/docs/android/setup
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseVersion"

    // Firebase - Crash Reporting
    // https://firebase.google.com/docs/crash/android
    implementation "com.google.firebase:firebase-crash:$rootProject.firebaseVersion"

    // Butterknife - android-view injection
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"
}


// **IMPORTANT** Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'